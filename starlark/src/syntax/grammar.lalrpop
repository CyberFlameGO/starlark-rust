// Copyright 2018 The Starlark in Rust Authors.
// Copyright (c) Facebook, Inc. and its affiliates.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

use codemap::{Span, CodeMap};
use std::sync::Arc;
use crate::syntax::lexer;
use crate::syntax::dialect::Dialect;
use crate::syntax::ast::*;

grammar(codemap: &Arc<CodeMap>, file_span: Span, dialect: &Dialect);

#[inline]
ASTS<E>: AstStmt = <l:@L> <e:E> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
ASTE<E>: AstExpr = <l:@L> <e:E> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
ASTP<E>: AstParameter = <l:@L> <e:E> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
ASTA<E>: AstArgument = <l:@L> <e:E> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
ASTC<E>: AstClause = <l:@L> <e:E> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
integer: AstInt = <l:@L> <e:"INTEGER"> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
string: AstString = <l:@L> <e:"STRING"> <r:@R>
    => e.to_ast(file_span, l, r);

#[inline]
identifier: AstString = <l:@L> <e:"IDENTIFIER"> <r:@R>
    => e.to_ast(file_span, l, r);

COMMA<E>: Vec<E> =
    <v0:(<E> ",")*> <e1:E?>
        => v0.into_iter().chain(e1).collect();

pub(crate) Starlark: AstStmt = ASTS<starlark_>;
starlark_: Stmt = "\n"* <(<Stmt> "\n"*)*>
    => Stmt::Statements(<>);

DefStmt: AstStmt = ASTS<DefStmt_> =>? Ok(dialect.check_def(codemap, <>)?);
DefStmt_: Stmt =
  "def" <identifier> "(" <COMMA<ParameterTyped>> ")" <ReturnType> ":" <Suite>
      =>? Ok(Stmt::check_def(<>, codemap)?);

ReturnType: Option<Box<AstExpr>> = {
    "->" <Test> =>? Ok(Some(box dialect.check_type(codemap, <>)?)),
    => None,
}

Parameter: AstParameter = ASTP<Parameter_>;
Parameter_: Parameter = {
    <n:identifier> "=" <e:Test> => Parameter::WithDefaultValue(n, None, box e),
    <identifier>            => Parameter::Normal(<>, None),
    "*" <identifier>        => Parameter::Args(<>, None),
    <l:@L> "*" <r:@R>       =>? Ok(dialect.check_keyword_only_arguments(codemap, file_span.subspan(l, r), Parameter::NoArgs)?),
    "**" <identifier>       => Parameter::KWArgs(<>, None),
};

ParameterTyped: AstParameter = ASTP<ParameterTyped_>;
ParameterTyped_: Parameter = {
    <n:identifier> <t:Type> "=" <e:Test> => Parameter::WithDefaultValue(n, t, box e),
    <identifier> <Type>            => Parameter::Normal(<>),
    "*" <identifier> <Type>        => Parameter::Args(<>),
    <l:@L> "*" <r:@R>              =>? Ok(dialect.check_keyword_only_arguments(codemap, file_span.subspan(l, r), Parameter::NoArgs)?),
    "**" <identifier> <Type>       => Parameter::KWArgs(<>),
};

Type: Option<Box<AstExpr>> = {
    ":" <Test> =>? Ok(Some(box dialect.check_type(codemap, <>)?)),
    => None,
}

Suite: AstStmt = {
    SimpleStmt<SmallStmt>,
    "\n"+ "INDENT" <l:@L> "\n"* <v:(<Stmt> "\n"*)+> <r:@R> "DEDENT"
        => Stmt::Statements(v).to_ast(file_span, l, r)
};

Stmt: AstStmt = { DefStmt, IfStmt, ForStmt, SimpleStmt<SmallStmt> };

IfBody: AstStmt = ASTS<IfBody_>;
IfBody_: Stmt = <c:Test> ":" <s:Suite> <el:ElseStmt?> => {
    match el {
        None   => Stmt::If(c, box s),
        Some(e) => Stmt::IfElse(c, box (s, e))
    }
};

IfStmt: AstStmt = ASTS<IfStmt_>;
IfStmt_: Stmt = "if" <IfBody_>;

ElseStmt: AstStmt = {
    "elif" <IfBody>,
    "else" ":" <Suite>
};

ForStmt: AstStmt = ASTS<ForStmt_>;
ForStmt_: Stmt = "for" <e:ExprList> "in" <c:Test> ":" <s:Suite>
    => Stmt::For(box (e, c, s));

SimpleStmt<S>: AstStmt =
    <l:@L> <e:S> <v:(";" <S>)*> ";"? <r:@R> "\n" => {
        if v.is_empty() {
            e
        } else {
            Stmt::Statements(
                vec![e].into_iter().chain(v.into_iter()).collect())
                    .to_ast(file_span, l, r)
        }
    };

SmallStmt: AstStmt = {
    <l:@L> "return" <e:TestList?> <r:@R>
        => Stmt::Return(e).to_ast(file_span, l, r),
    <@L> "break" <@R>
        => Stmt::Break.to_ast(file_span, <>),
    <@L> "continue" <@R>
        => Stmt::Continue.to_ast(file_span, <>),
    <@L> "pass" <@R>
        => Stmt::Pass.to_ast(file_span, <>),
    AssignStmt,
    ExprStmt,
    LoadStmt,
};

AssignOp: AssignOp = {
    "=" => AssignOp::Assign,
    "+=" => AssignOp::Increment,
    "-=" => AssignOp::Decrement,
    "*=" => AssignOp::Multiplier,
    "//=" => AssignOp::FloorDivider,
    "%=" => AssignOp::Percent,
};

AssignStmt: AstStmt = ASTS<AssignStmt_>;
AssignStmt_: Stmt = <lhs:TestList> <op:AssignOp> <rhs:TestList>
        => Stmt::Assign(box lhs, op, box rhs);

// In python ExprStmt is an AssignStmt (
// https://docs.python.org/3/reference/grammar.html). This ExprStmt is
// according to the spec provided on https://github.com/google/skylark. It
// enable parsing docstring and method calls.
ExprStmt: AstStmt = ASTS<ExprStmt_>;
ExprStmt_: Stmt = <Test> => Stmt::Expression(<>);

LoadStmt: AstStmt = ASTS<LoadStmt_> =>? Ok(dialect.check_load(codemap, <>)?);
LoadStmt_: Stmt = "load" "(" <string> <("," <LoadStmtSyms>)+> ","? ")"
    => Stmt::Load(<>, dialect.load_visibility());

LoadStmtBindingName: AstString = <identifier> "=";

LoadStmtSyms: (AstString, AstString) = <id:LoadStmtBindingName?> <n:string>
    => (id.unwrap_or(n.clone()), n);

// Expression
L<E>: AstExpr = <l:@L> <v:(<E> ",")*> <e:E> <f:","?> <r:@R>
    => {
        if f.is_some() || !v.is_empty() {
            Expr::Tuple(v.into_iter().chain(vec![e].into_iter()).collect())
                .to_ast(file_span, l, r)
        } else {
            e
        }
    };

ExprList: AstExpr = L<Expr>;

TestList: AstExpr = L<Test>;

PipedExpr: AstExpr = {
    <l:@L> <e1:ArithExpr> "|" <e2:PipedExpr> <r:@R>
      => Expr::Op(box e1, BinOp::Pipe, box e2).to_ast(file_span, l, r),
    ArithExpr
};

PrimaryExpr: AstExpr = {
    <l:@L> <e:PrimaryExpr> "." <i:identifier> <r:@R>
        => Expr::Dot(box e, i).to_ast(file_span, l, r),
    <l:@L> <e:PrimaryExpr> "(" <a:COMMA<Argument>> ")" <r:@R>
        =>? Ok(Expr::check_call(e, a, codemap)?.to_ast(file_span, l, r)),
    <l:@L> <e:PrimaryExpr> "[" <i1:Test?> ":" <i2:Test?> <i3:(":" <Test?>)?> "]"
        <r:@R> => {
          Expr::Slice(box e, i1.map(|x| box x), i2.map(|x| box x), i3.unwrap_or(None).map(|x| box x))
              .to_ast(file_span, l, r)
        },
    <l:@L> <e:PrimaryExpr> "[" <i:TestList> "]" <r:@R>
        => Expr::ArrayIndirection(box (e, i)).to_ast(file_span, l, r),
    Operand
};

OptionalSlice: AstExpr = ":" <Test>;

// Note that the order of arguments (args, named, *args, **kwargs) is enforced
// at the syntax evaluation, not by the Grammar.
Argument: AstArgument = ASTA<Argument_>;
Argument_: Argument = {
    <Test>                    => Argument::Positional(<>),
    <identifier> "=" <Test>   => Argument::Named(<>),
    "*" <Test>                => Argument::ArgsArray(<>),
    "**" <Test>               => Argument::KWArgsDict(<>)
};

Operand: AstExpr = {
    <l:@L> <i:identifier> <r:@R>
        => Expr::Identifier(i).to_ast(file_span, l, r),
    <l:@L> <i:integer> <r:@R>
        => Expr::Literal(AstLiteral::IntLiteral(i)).to_ast(file_span, l, r),
    <l:@L> <s:string> <r:@R>
        => Expr::Literal(AstLiteral::StringLiteral(s)).to_ast(file_span, l, r),
    <l:@L> "[" <e:COMMA<Test>> "]" <r:@R>
        => Expr::List(e).to_ast(file_span, l, r),
    ListComp,
    <l:@L> "{" <e:COMMA<DictEntry>> "}" <r:@R>
        => Expr::Dict(e).to_ast(file_span, l, r),
    DictComp,
    <l:@L> "(" <e:TestList?> ")" <r:@R>
        => match e {
            Some(t) => t,
            None => Expr::Tuple(vec![]).to_ast(file_span, l, r)
        }
};

DictEntry: (AstExpr, AstExpr) = <Test> ":" <Test> => (<>);

ListComp: AstExpr = ASTE<ListComp_>;
ListComp_: Expr = "[" <t:Test> <c:CompClause> "]"
    => Expr::ListComprehension(box t, c);

DictComp: AstExpr = ASTE<DictComp_>;
DictComp_: Expr = "{" <k:DictEntry> <v:CompClause>"}"
    => Expr::DictComprehension(box k, v);

CompClause: Vec<AstClause> = <CompIter+>;
CompIter: AstClause = ASTC<CompIter_>;
CompIter_: Clause = "for" <var:ExprList> "in" <over:OrTest> <ifs:IfClause*>
    => Clause {var, over, ifs};
IfClause: AstExpr = "if" <OrTest>;

// Base expression. Priorities are taken from Python 3 grammar.
Test: AstExpr = {
    <l:@L> <e1:OrTest> "if" <t:OrTest> "else" <e2:Test> <r:@R>
        => Expr::If(box (t, e1, e2)).to_ast(file_span, l, r),
    OrTest,
    LambDef
};

LambDef: AstExpr = {
    <l:@L> "lambda" <p:COMMA<Parameter>> ":" <e:Test> <r:@R>
        =>? Ok(dialect.check_lambda(codemap, Expr::Lambda(p, box e).to_ast(file_span, l, r))?),
}

// Binary operators
OrTest: AstExpr = {
    <l:@L> <e1:AndTest> "or" <e2:OrTest> <r:@R>
        => Expr::Op(box e1, BinOp::Or, box e2).to_ast(file_span, l, r),
    AndTest,
};

AndTest: AstExpr =  {
    <l:@L> <e1:NotTest> "and" <e2:AndTest> <r:@R>
        => Expr::Op(box e1, BinOp::And, box e2).to_ast(file_span, l, r),
    NotTest,
};

NotTest: AstExpr = {
  <l:@L> "not" <e:NotTest> <r:@R>
      => Expr::Not(box e).to_ast(file_span, l, r),
  CompTest,
};

CompTest: AstExpr = {
    <l:@L> <e1:Expr> "==" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::EqualsTo, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:Expr> "!=" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::Different, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:Expr> "<" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::LessThan, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:Expr> ">" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::GreaterThan, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:Expr> "<=" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::LessOrEqual, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:Expr> ">=" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::GreaterOrEqual, box e2)
                      .to_ast(file_span, l, r),
    <l:@L> <e1:Expr> "in" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::In, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:Expr> "not" "in" <e2:CompTest> <r:@R>
        => Expr::Op(box e1, BinOp::NotIn, box e2).to_ast(file_span, l, r),
    Expr
};

Expr: AstExpr = {
    <l:@L> <e1:ArithExpr> "|" <e2:Expr> <r:@R>
        => Expr::Op(box e1, BinOp::Pipe, box e2).to_ast(file_span, l, r),
    ArithExpr,
};

ArithExpr: AstExpr = {
    <l:@L> <e1:ArithExpr> "+" <e2:ProductExpr> <r:@R>
        => Expr::Op(box e1, BinOp::Addition, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:ArithExpr> "-" <e2:ProductExpr> <r:@R>
        => Expr::Op(box e1, BinOp::Subtraction, box e2).to_ast(file_span, l, r),
    ProductExpr,
};

ProductExpr: AstExpr = {
    <l:@L> <e1:ProductExpr> "*" <e2:FactorExpr> <r:@R>
        => Expr::Op(box e1, BinOp::Multiplication, box e2)
            .to_ast(file_span, l, r),
    <l:@L> <e1:ProductExpr> "%" <e2:FactorExpr> <r:@R>
        => Expr::Op(box e1, BinOp::Percent, box e2).to_ast(file_span, l, r),
    <l:@L> <e1:ProductExpr> "//" <e2:FactorExpr> <r:@R>
        => Expr::Op(box e1, BinOp::FloorDivision, box e2).to_ast(file_span, l, r),
    FactorExpr
};

FactorExpr: AstExpr = {
    <l:@L> "+" <e:FactorExpr> <r:@R>
        => Expr::Plus(box e).to_ast(file_span, l, r),
    <l:@L> "-" <e:FactorExpr> <r:@R>
        => Expr::Minus(box e).to_ast(file_span, l, r),
    PrimaryExpr
};

extern {
    type Location = u64;
    type Error = anyhow::Error;

    enum lexer::Token {
      "INDENT" => lexer::Token::Indent,
      "DEDENT" => lexer::Token::Dedent,
      "\n"     => lexer::Token::Newline,
      // Keywords
      "and" => lexer::Token::And,
      "else" => lexer::Token::Else,
      "load" => lexer::Token::Load,
      "break" => lexer::Token::Break,
      "for" => lexer::Token::For,
      "not" => lexer::Token::Not,
      "continue" => lexer::Token::Continue,
      "if" => lexer::Token::If,
      "or" => lexer::Token::Or,
      "def" => lexer::Token::Def,
      "in" => lexer::Token::In,
      "pass" => lexer::Token::Pass,
      "elif" => lexer::Token::Elif,
      "return" => lexer::Token::Return,
      "lambda" => lexer::Token::Lambda,
      // Symbols
      "," => lexer::Token::Comma,
      ";" => lexer::Token::Semicolon,
      ":" => lexer::Token::Colon,
      "+=" => lexer::Token::PlusEqual,
      "-=" => lexer::Token::MinusEqual,
      "*=" => lexer::Token::StarEqual,
      "//=" => lexer::Token::SlashSlashEqual,
      "%=" => lexer::Token::PercentEqual,
      "==" => lexer::Token::EqualEqual,
      "!=" => lexer::Token::BangEqual,
      "<=" => lexer::Token::LessEqual,
      ">=" => lexer::Token::GreaterEqual,
      "**" => lexer::Token::StarStar,
      "->" => lexer::Token::RightArrow,
      "=" => lexer::Token::Equal,
      "<" => lexer::Token::LessThan,
      ">" => lexer::Token::GreaterThan,
      "-" => lexer::Token::Minus,
      "+" => lexer::Token::Plus,
      "*" => lexer::Token::Star,
      "%" => lexer::Token::Percent,
      "//" => lexer::Token::SlashSlash,
      "." => lexer::Token::Dot,
      "|" => lexer::Token::Pipe,
      // Brackets
      "[" => lexer::Token::OpeningSquare,
      "{" => lexer::Token::OpeningCurly,
      "(" => lexer::Token::OpeningRound,
      "]" => lexer::Token::ClosingSquare,
      "}" => lexer::Token::ClosingCurly,
      ")" => lexer::Token::ClosingRound,


      "RESERVED" => lexer::Token::Reserved(<String>),
      "IDENTIFIER" => lexer::Token::Identifier(<String>),
      "INTEGER" => lexer::Token::IntegerLiteral(<i32>),
      "STRING" => lexer::Token::StringLiteral(<String>)
    }
}
